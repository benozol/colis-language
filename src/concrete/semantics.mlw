
module Filesystem
  type filesystem
  val constant empty: filesystem
end

module Env
  use map.Map
  use syntax.Variable
  use mystring.String
  
  type t 'a = map variable 'a

  let constant empty =
    fun _ -> String.empty (* Cannot use map.Const because Map__Const is not extracted... *)

  let function ([<-]) (e:t 'a) (var:variable) (value:'a) : variable -> 'a
    ensures { result[var] = value }
    ensures { forall var'. var' <> var -> result[var'] = e[var'] }
  = fun var' ->
      if variable_eq var' var
      then value
      else e var'
end

module Behaviour

  use option.Option
  use bool.Bool

  (** Strict evaluation mode ("false is fatal") applies only outside of
      conditions, see NOTES[Strict] *)
  let function strict under_condition =
    notb under_condition

  (* No functions (see NOTES[Functions]), no return-behaviour *)
  type behaviour =
    | BNormal bool
    | BExit bool

  let function bool_of_behaviour (b:behaviour) : bool =
    match b with
    | BNormal b'
    | BExit b' -> b'
    end

  let function bool_opt_compose (bo1 bo2: option bool) : option bool =
    match bo2 with
    | Some _ -> bo2
    | None -> bo1
    end
end

module Buffers
  use list.List
  use list.Append
  use list.Reverse
  use mystring.String

  (* stdin and stdout are comprised of a list of lines, cf. NOTES[Stdin/stdout] *)

  scope Stdin
    (* The list contains the lines of the stdin, to be read in-order *)
    type t = list string

    let constant empty : t = Nil
  end

  type stdin = Stdin.t

  scope Stdout

    (** The current line and the previous lines, with the most recent line first.

        Keeping the current line and the previous lines explicitely separated (instead of
        a single list with is non-empty by invariant) simplifies stating the below lemmas
     *)
    type t = {line: string; lines: list string}

    let function all_lines out =
      Cons out.line out.lines

    let constant empty =
      { line = String.empty; lines = Nil }

    (** Pipe stdout to stdin (last line is lost: NOTES[Stdin/stdout] *)
    let function to_stdin out =
      reverse (all_lines out)

    let output str out =
      { out with line = out.line^str }

    let newline out =
      { line = String.empty; lines = Cons out.line out.lines }

    (* http://pubs.opengroup.org/onlinepubs/9699919799/utilities/V3_chap02.html#tag_18_06_03 *)
    let function to_string out : string =
      let rec remove_trailing_newlines lines variant { lines } =
        match lines with
        | Nil -> Nil
        | Cons l lines' ->
          if String.(equal l empty)
          then remove_trailing_newlines lines'
          else lines
        end
      in
      concat_lines (reverse (remove_trailing_newlines out.lines))

    (* Concatenate two stdouts (see NOTES[Stdin/stdout]).

         concat_stdout [""; "S2"; "S1"] [""; "S3"] -> concat_stdout [""; "S3; "S2"; "S1"]

       and when final lines are not closed (using `echo -n S3`):

         concat_stdout ["S3"; "S2"; "S1"] ["S5"; "S4"] -> concat_stdout ["S5"; "S3S4; "S2"; "S1"]

       Ah, here the complexity for specifying `read` that we avoided by using `list string`
       re-emerges!
    *)
    let rec function concat out1 out2
      variant { out2.lines }
      (* ensures { concat_rel out1 out2 result } *)
    = match out2.lines with
      | Nil -> {line=out1.line^out2.line; lines= out1.lines}
      | Cons line2 lines2 ->
        let out3 = concat out1 {line=line2; lines=lines2} in
        {line=out2.line; lines=all_lines out3}
      end

    let rec lemma concat_empty_left (out:t)
      variant { out.lines }
      ensures {
        concat empty out = out
      }
    = match out.lines with
      | Nil -> ()
      | Cons l ls -> concat_empty_left {line=l; lines=ls}
      end


    let rec lemma concat_empty_right (out:t)
      variant { out.lines }
      ensures {
        concat out empty = out
      }
    = match out.lines with
      | Nil -> ()
      | Cons l ls -> concat_empty_right {line=l; lines=ls}
      end

    let rec lemma concat_aux (out2:t)
      variant { out2.lines }
      ensures { forall out1 line3.
        let out12 = concat out1 out2 in
        concat out1 { line = out2.line^line3; lines = out2.lines }  = 
          { line = out12.line^line3; lines = out12.lines }
      }
    = match out2.lines with
      | Nil -> ()
      | Cons line2 lines2 -> concat_aux { line = line2; lines = lines2 }
      end

    let rec lemma concat_assoc out3
      variant { out3.lines }
      ensures { forall out1 out2.
        concat out1 (concat out2 out3) =
          concat (concat out1 out2) out3
      }
    = match out3.lines with
      | Nil ->
        assert { forall out2.
          concat out2 out3 =
            { line = out2.line^out3.line; lines = out2.lines }
        }
      | Cons line3 lines3 ->
        concat_assoc {line = line3; lines = lines3}
      end
  end

  type stdout = Stdout.t
end

(** {2 Contexts for the evaluation}

    NOTES[Evaluation relation]
 *)

module Context

  use array.Array
  use map.Map
  use bool.Bool
  use list.List
  use list.Reverse
  use list.Append

  use mystring.String
  use syntax.Variable

  use Filesystem
  use Behaviour
  use Buffers
  use Env

  (** {3 Read-only context for the evaluation of a term} *)

  type input = {
    arguments: array string;
    under_condition: bool; (* evaluation under condition - implying unstrict mode *)
    previous_result: bool; (* result of last command, i.e. `$?`, default `True` *)
  }

  let constant empty_input = {
    arguments = Array.make 0 String.empty;
    previous_result = True;
    under_condition = False;
  }

  (** {3 Read/write context for the evaluation of a term} *)

  (** The state is unreversable ("following a linear logic") and each internal state should
      be consumed exactly once *)
  type state = {
    filesystem: filesystem;
    stdin: stdin;
    stdout: stdout;
  }

  let function mk_state fs in_ out
    (* ensures { result = mk state fs in_ out } *)
    ensures { result.filesystem = fs }
    ensures { result.stdin = in_ }
    ensures { result.stdout = out }
  = { filesystem = fs; stdin = in_; stdout = out }

  let constant empty_state = {
    filesystem = Filesystem.empty;
    stdin = Stdin.empty;
    stdout = Stdout.empty;
  }

  (** The context of the interpretation, resulting contexts may be ignored (e.g., from
      subprocesses)

      Will contain also the function environment one functions are added to the language
   *)

  type context = {
    senv: Env.t string;
  }

  let constant empty_context = {
    senv = Env.empty; 
  }
end

(** {2 Semantics of the CoLiS language} *)

module Semantics

  use list.List
  use list.Append
  use option.Option
  use map.Map
  use bool.Bool

  use mystring.String
  use syntax.Syntax

  use Behaviour
  use Context
  use Buffers

  let function bool_of_return_code (c:return_code) (previous_result:bool) : bool =
    match c with
    | RSuccess -> True
    | RFailure -> False
    | RPrevious -> previous_result
    end

  let function inject_behaviour (inp:input) (ret:bool) : behaviour =
    if andb (strict inp.under_condition) (notb ret)
    then BExit False
    else BNormal ret

  let function opt_inject_behaviour (inp:input) (ret_opt:option bool) : behaviour =
    match ret_opt with
    | Some ret -> inject_behaviour inp ret
    | None -> BNormal True
    end

  let function option_get default opt =
    match opt with Some x -> x | None -> default end

  (** Interprete a command defined in the document *Specification of UNIX Commands*.

      It acts upon the input and irreversable state, and returns a stdout, a behaviour
      (True/False), and another irreversable state, see also NOTES[Callees] *)
  val function interp_builtin state string (list string) : (state, bool)

  axiom interp_builtin_extends_output : forall sta sta' name args b.
    interp_builtin {sta with stdout=Stdout.empty} name args = (sta', b) ->
    interp_builtin sta name args = ({sta' with stdout=Stdout.concat sta.stdout sta'.stdout}, b)

  (** Evaluation of a instruction i with input I, context C and state S resulting in an
      state S', context C', and behaviour b:

      $(I, C, S) i \Downarrow (S', C', b)$
   *)

  inductive eval_instruction input context state instruction state context behaviour =

  | eval_exit: forall inp ctx sta c.
    let b = bool_of_return_code c inp.previous_result in
    let bhv = BExit b in
    eval_instruction inp ctx sta (IExit c) sta ctx bhv

  | eval_assignment: forall inp ctx sta sta' v e s bo.
    eval_str_expr inp ctx sta e sta' s bo ->
    let ctx' = {ctx with senv = ctx.senv[v <- s]} in
    let bhv = inject_behaviour inp (option_get True bo) in
    eval_instruction inp ctx sta (IAssignment v e) sta' ctx' bhv
  
  | eval_sequence_normal: forall inp sta sta1 sta2 ins1 ins2 ctx ctx1 ctx2 b1 bhv2.
    eval_instruction inp ctx sta ins1 sta1 ctx1 (BNormal b1) ->
    eval_instruction {inp with previous_result = b1} ctx1 sta1 ins2 sta2 ctx2 bhv2 ->
    eval_instruction inp ctx sta (ISequence ins1 ins2) sta2 ctx2 bhv2
  
  | eval_sequence_abort: forall inp sta sta1 ctx ctx1 ins1 ins2 b.
    let bhv = BExit b in
    eval_instruction inp ctx sta ins1 sta1 ctx1 bhv ->
    eval_instruction inp ctx sta (ISequence ins1 ins2) sta1 ctx1 bhv
  
  | eval_subshell: forall inp sta sta' ctx ctx' ins bhv.
    eval_instruction inp ctx sta ins sta' ctx' bhv ->
    (* `Normal false` turns to `Exit False` in strict mode - the line `set -e; (! true);
       echo A/$?` prints nothing *)
    let bhv' = inject_behaviour inp (bool_of_behaviour bhv) in
    eval_instruction inp ctx sta (ISubshell ins) sta' ctx bhv'

  | eval_not: forall inp ctx ctx' sta sta' ins b.
    eval_instruction {inp with under_condition = True} ctx sta ins sta' ctx' (BNormal b) ->
    let bhv' = BNormal (notb b) in
    eval_instruction inp ctx sta (INot ins) sta' ctx' bhv'

  | eval_not_transmit: forall inp ctx ctx' sta sta' ins b.
    let bhv = BExit b in
    eval_instruction {inp with under_condition = True} ctx sta ins sta' ctx' bhv ->
    eval_instruction inp ctx sta (INot ins) sta' ctx' bhv
  
  | eval_if_true: forall inp ctx ctx1 ctx2 sta sta1 sta2 ins1 ins2 ins3 bhv2.
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 (BNormal True) ->
    eval_instruction {inp with previous_result = True} ctx1 sta1 ins2 sta2 ctx2 bhv2 ->
    eval_instruction inp ctx sta (IIf ins1 ins2 ins3) sta2 ctx2 bhv2
  
  | eval_if_false: forall inp ctx ctx1 ctx3 sta sta1 sta3 ins1 ins2 ins3 bhv3.
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 (BNormal False) ->
    eval_instruction {inp with previous_result = False} ctx1 sta1 ins3 sta3 ctx3 bhv3 ->
    eval_instruction inp ctx sta (IIf ins1 ins2 ins3) sta3 ctx3 bhv3
  
  | eval_if_transmit: forall inp ctx ctx1 sta sta1 ins1 ins2 ins3 b.
    let bhv1 = BExit b in
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 bhv1 ->
    eval_instruction inp ctx sta (IIf ins1 ins2 ins3) sta1 ctx1 bhv1
  
  | eval_nooutput: forall inp ctx ctx' sta sta' ins bhv.
    eval_instruction inp ctx sta ins sta' ctx' bhv ->
    let sta'' = {sta' with stdout = sta.stdout} in
    eval_instruction inp ctx sta (INoOutput ins) sta'' ctx' bhv
  
  (** See NOTES[Pipe semantics] *)
  | eval_pipe: forall inp ctx ctx1 ctx2 sta sta1 sta2 ins1 ins2 bhv1 bhv2.
    let sta' = {sta with stdout = Stdout.empty} in
    eval_instruction inp ctx sta' ins1 sta1 ctx1 bhv1 ->
    let sta1' = {sta1 with stdout = sta.stdout; stdin = Stdout.to_stdin sta1.stdout} in
    eval_instruction inp ctx sta1' ins2 sta2 ctx2 bhv2 ->
    eval_instruction inp ctx sta (IPipe ins1 ins2) sta2 ctx bhv2
  
  | eval_call: forall inp ctx sta sta' sta'' s le ss b.
    eval_list_expr inp ctx sta le sta' ss ->
    (sta'', b) = interp_builtin sta' s ss ->
    let bhv = inject_behaviour inp b in
    eval_instruction inp ctx sta (ICall s le) sta'' ctx bhv
  
  | eval_foreach: forall inp ctx ctx' sta sta' sta'' v es ins ss bhv.
    eval_list_expr inp ctx sta es sta' ss -> 
    (* The behaviour of empty for-loops is `BNormal True` *)
    eval_foreach (BNormal True) inp ctx sta' v ss ins sta'' ctx' bhv ->
    eval_instruction inp ctx sta (IForeach v es ins) sta'' ctx' bhv

  | eval_while: forall inp ctx ctx' sta sta' ins1 ins2 bhv.
    (* The behaviour of empty while-loops is `BNormal True` *)
    eval_while (BNormal True) inp ctx sta ins1 ins2 sta' ctx' bhv ->
    eval_instruction inp ctx sta (IWhile ins1 ins2) sta' ctx' bhv

  (* The first parameter is the previous behaviour, or the behaviour of the empty loop initially *)
  with eval_while behaviour input context state instruction instruction state context behaviour =

  (** while-condition is false *)
  | eval_while_false: forall bhv inp ctx ctx1 sta sta1 ins1 ins2.
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 (BNormal False) ->
    eval_while bhv inp ctx sta ins1 ins2 sta1 ctx1 bhv

  (** while-condition is not normal *)
  | eval_while_transmit: forall bhv inp ctx ctx1 sta sta1 ins1 ins2 b.
    let bhv1 = BExit b in
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 bhv1 ->
    eval_while bhv inp ctx sta ins1 ins2 sta1 ctx1 bhv1

  (** while-condition true but while-body not normal *)
  | eval_while_true_transmit: forall bhv inp ctx ctx1 ctx2 sta sta1 sta2 ins1 ins2 b.
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 (BNormal True) ->
    let bhv2 = BExit b in
    eval_instruction inp ctx1 sta1 ins2 sta2 ctx2 bhv2 ->
    eval_while bhv inp ctx sta ins1 ins2 sta2 ctx2 bhv2

  (** while-condition true, body normal, and loop again *)
  | eval_while_true_loop: forall bhv inp ctx ctx1 ctx2 ctx3 sta sta1 sta2 sta3 ins1 ins2 b bhv3.
    eval_instruction {inp with under_condition = True} ctx sta ins1 sta1 ctx1 (BNormal True) ->
    eval_instruction inp ctx1 sta1 ins2 sta2 ctx2 (BNormal b) ->
    eval_while (BNormal b) {inp with previous_result = b} ctx2 sta2 ins1 ins2 sta3 ctx3 bhv3 ->
    eval_while bhv inp ctx sta ins1 ins2 sta3 ctx3 bhv3

  (* The first parameter is the previous behaviour, or the behaviour of the empty loop initially *)
  with eval_foreach behaviour input context state variable (list string) instruction state context behaviour =

  | eval_foreach_done: forall bhv inp ctx sta var ins.
    eval_foreach bhv inp ctx sta var Nil ins sta ctx bhv

  | eval_foreach_step: forall bhv inp ctx ctx1 ctx2 sta sta1 sta2 var s ss' ins bhv1 bhv2.
    let ss = Cons s ss' in
    eval_instruction inp {ctx with senv = ctx.senv[var <- s]} sta ins sta1 ctx1 bhv1 ->
    eval_foreach bhv1 inp ctx1 sta1 var ss' ins sta2 ctx2 bhv2 ->
    eval_foreach bhv inp ctx sta var ss ins sta2 ctx2 bhv2

  (** Evaluation of an expression with some input I, context C, and state S, resulting in
      another state S, a string and an output comprised of a string result and an optional
      boolean behaviour (only $(...) has a behaviour):

      $(Input, Context, State) e \Downarrow (State, String, OptBool)$
   *)
  with eval_str_expr input context state string_expression state string (option bool)  =

  | eval_str_literal: forall inp ctx sta s. 
    eval_str_expr inp ctx sta (SLiteral s) sta s None

  | eval_str_variable: forall inp ctx sta v.
    let s = ctx.senv[v] in
    eval_str_expr inp ctx sta (SVariable v) sta s None

  | eval_str_subshell: forall inp ctx ctx' sta sta2 stm bhv.
    let sta1 = {sta with stdout = Stdout.empty} in
    eval_instruction inp ctx sta1 stm sta2 ctx' bhv ->
    let sta3 = {sta2 with stdout = sta.stdout} in
    let s = Stdout.to_string sta2.stdout in
    let bo = Some (bool_of_behaviour bhv) in
    eval_str_expr inp ctx sta (SSubshell stm) sta3 s bo

  | eval_str_concat : forall inp ctx sta sta1 sta2 e1 e2 s1 s2 bo1 bo2.
    eval_str_expr inp ctx sta e1 sta1 s1 bo1 ->
    eval_str_expr inp ctx sta1 e2 sta2 s2 bo2 ->
    let bo3 = bool_opt_compose bo1 bo2 in
    let s3 = s1 ^ s2 in
    eval_str_expr inp ctx sta (SConcat e1 e2) sta2 s3 bo3

  (** Evaluation of expressions to a list of strings:

      $(Bool, I, C, S) es \Downarrow (S, StringList)$
   *)
  with eval_list_expr input context state list_expression state (list string) =

    | eval_list_expr_nil: forall inp ctx sta.
      eval_list_expr inp ctx sta Nil sta Nil

    | eval_list_expr_cons: forall inp ctx sta sta1 sta2 se sp es s bo l2.
      eval_str_expr inp ctx sta se sta1 s bo ->
      let l1 = match sp with Split -> String.split_on_default_ifs s | DontSplit -> Cons s Nil end in
      eval_list_expr inp ctx sta1 es sta2 l2 ->
      let l3 = l1 ++ l2 in
      eval_list_expr inp ctx sta (Cons (se, sp) es) sta2 l3


  (** Evaluation of a program. It outputs in a boolean value *)
  inductive eval_program input context state program state context bool =
    | eval_program_instruction: forall inp ctx ctx' sta sta' pro bhv.
      eval_instruction inp ctx sta pro sta' ctx' bhv ->
      let b = bool_of_behaviour bhv in
      eval_program inp ctx sta pro sta' ctx' b

  lemma eval_instruction_functional: forall ins[@induction] inp (*out1 out2*) ctx ctx1 ctx2 sta sta1 sta2 bhv1 bhv2.
    eval_instruction inp ctx sta (* { sta with stdout = out1 } *) ins sta1 ctx1 bhv1 ->
    eval_instruction inp ctx sta (* { sta with stdout = out2 } *) ins sta2 ctx2 bhv2 ->
    sta1 = sta2 /\ ctx1 = ctx2 /\ bhv1 = bhv2

  (** The evaluation of a string expression does not change the stdout *)
  lemma eval_str_expr_keeps_stdout: forall se[@induction] inp ctx sta sta' out s bo.
    eval_str_expr inp ctx { sta with stdout = out } se sta' s bo ->
    sta'.stdout = out

  lemma eval_str_expr_extends_stdout: forall se[@induction] inp ctx sta sta' s bo.
      eval_str_expr inp ctx { sta with stdout = Stdout.empty } se sta' s bo ->
      eval_str_expr inp ctx sta se { sta' with stdout = Stdout.concat sta.stdout sta'.stdout } s bo

  (** The evaluation of a list expression does not change the stdout *)
  lemma eval_list_expr_keeps_stdout: forall le[@induction] inp ctx sta sta' ss out.
    eval_list_expr inp ctx { sta with stdout = out } le sta' ss ->
    sta'.stdout = out

  lemma eval_list_expr_expands_stdout: forall le[@induction] inp ctx sta sta' ss.
    eval_list_expr inp ctx { sta with stdout = Stdout.empty } le sta' ss ->
    eval_list_expr inp ctx sta le { sta' with stdout = Stdout.concat sta.stdout sta'.stdout } ss

  (** Foreach loops expand stdout, if the loop body expands also *)
  lemma eval_foreach_expands_stdout: forall ss[@induction] bhv bhv' inp ctx ctx' sta sta' var ins.
    (forall inp ctx ctx' sta sta' bhv.
      eval_instruction inp ctx { sta with stdout = Stdout.empty } ins sta' ctx' bhv ->
      eval_instruction inp ctx sta ins { sta' with stdout = Stdout.concat sta.stdout sta'.stdout } ctx' bhv) ->
    eval_foreach bhv inp ctx { sta with stdout = Stdout.empty } var ss ins sta' ctx' bhv' ->
    eval_foreach bhv inp ctx sta var ss ins { sta' with stdout = Stdout.concat sta.stdout sta'.stdout } ctx' bhv'

  lemma eval_call_expands_stdout: forall inp ctx sta sta' sta'' s le ss b.
    eval_list_expr inp ctx { sta with stdout = Stdout.empty } le sta' ss ->
    (sta'', b) = interp_builtin { sta' with stdout = Stdout.empty } s ss ->
    let bhv = inject_behaviour inp b in
    let out = Stdout.(concat sta.stdout (concat sta'.stdout sta''.stdout)) in
    eval_instruction inp ctx sta (ICall s le) { sta'' with stdout = out } ctx bhv

  lemma eval_sequence_expands_stdout: forall ins1 ins2[@induction].
    (forall inp ctx ctx' sta sta' bhv.
      eval_instruction inp ctx { sta with stdout = Stdout.empty } ins1 sta' ctx' bhv ->
      eval_instruction inp ctx sta ins1 { sta' with stdout = Stdout.concat sta.stdout sta'.stdout } ctx' bhv) ->
    (forall inp ctx ctx' sta sta' bhv.
      eval_instruction inp ctx { sta with stdout = Stdout.empty } ins2 sta' ctx' bhv ->
      eval_instruction inp ctx sta ins2 { sta' with stdout = Stdout.concat sta.stdout sta'.stdout } ctx' bhv) ->
    forall inp ctx ctx1 ctx2 sta sta1 sta2 b1 bhv2.
    eval_instruction inp ctx { sta with stdout = Stdout.empty } ins1 sta1 ctx1 (BNormal b1) ->
    eval_instruction inp ctx1 { sta1 with stdout = Stdout.empty } ins2 sta2 ctx2 bhv2 ->
    let out = Stdout.(concat sta.stdout (concat sta1.stdout sta2.stdout)) in
    eval_instruction inp ctx sta (ISequence ins1 ins2) { sta2 with stdout = out } ctx2 bhv2

  lemma eval_instruction_extends_stdout: forall ins[@induction] inp ctx ctx' sta sta' bhv.
      eval_instruction inp ctx {sta with stdout=Stdout.empty} ins sta' ctx' bhv ->
      eval_instruction inp ctx sta ins {sta' with stdout=Stdout.concat sta.stdout sta'.stdout} ctx' bhv
end